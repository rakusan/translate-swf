/*
 * AS1PArser.jj
 * Translate SWF
 *
 * A compiler for ActionScript
 * Copyright (c) 2010 Flagstone Software Ltd. All rights reserved.
 *
 * Redistribution and use in source and binary forms, with or without modification,
 * are permitted provided that the following conditions are met:
 *
 *  * Redistributions of source code must retain the above copyright notice, this
 *    list of conditions and the following disclaimer.
 *  * Redistributions in binary form must reproduce the above copyright notice,
 *    this list of conditions and the following disclaimer in the documentation
 *    and/or other materials provided with the distribution.
 *  * Neither the name of Flagstone Software Ltd. nor the names of its contributors
 *    may be used to endorse or promote products derived from this software
 *    without specific prior written permission.
 *
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS" AND
 * ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED
 * WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED.
 * IN NO EVENT SHALL THE COPYRIGHT OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT,
 * INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING,
 * BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
 * DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF
 * LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE
 * OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED
 * OF THE POSSIBILITY OF SUCH DAMAGE.

 */
 
options {
    STATIC = false;
	JAVA_UNICODE_ESCAPE = true;
	FORCE_LA_CHECK = true;
}

PARSER_BEGIN(AS1Parser)

package com.flagstone.translate.as1;

import java.io.*;
import java.util.List;
import java.util.ArrayList;

import com.flagstone.transform.Event;
import com.flagstone.translate.Parser;
import com.flagstone.translate.ParserProvider;
import com.flagstone.translate.Node;
import com.flagstone.translate.EventNode;
import com.flagstone.translate.NodeType;
import com.flagstone.translate.ScriptError;

public final class AS1Parser implements ParserProvider, Parser {

    private transient String path;
    private transient List<ScriptError>errors;
    
    /**
     * Constructs a new parser object. The current working directory "." is added 
     * to the list of directories that will be searched when looking for a file.
     */
    public AS1Parser() {
        this(System.in);
    }
    
    public Parser newParser() {
		return new AS1Parser();
	}
    
    public void setPath(final String str) {
        path = str;
    }
    
    public void setErrors(final List<ScriptError> list) {
        errors = list;
    }

    /**
     * Parses Actionscript from a stream.
     *
     * @param stream an InputStream containing the ActionScript code to parse.
     *
     * @throws ParseException if a parsing error occurs.
     */
    public Node parse(final InputStream stream) {
    	Node root = null;
        try {
            ReInit(stream);
        	root = Script();
        } catch (ParseException e) {
            errors.add(new ScriptError(ScriptError.Type.SCRIPT_PARSE_ERROR,
                        path, token.beginLine, token.endLine,
                        token.beginColumn, token.endColumn));
        }
        return root;
    }
    
    /**
     * reportError is used to report any errors found when validating a node 
     * during parsing. A ParseException is created containing the key which 
     * identifies the error and the Token which triggered it.
     *
     * @param errorKey a String that identifies the type of error that occurred.
     * @param token the token being parsed which triggered the error.
     *
     * @throws ParseException containing the errorKey and line number.
     */
    private void reportError(String errorKey, Token token) 
            throws ParseException {
        ParseException parseError = new ParseException(errorKey);
        parseError.currentToken = token;
        throw parseError;
    }
}

PARSER_END(AS1Parser)

SKIP : /* WHITE SPACE */
{
      " "
    | "\t"
    | "\f"
    | "\r"
    | "\n"
    | <"//" (~["\n","\r"])* ("\n" | "\r" | "\r\n") >
    | <"/*" (~["*"])* "*" ("*" | ~["*","/"] (~["*"])* "*")* "/">
}

TOKEN : /* LITERALS */
{
      <BOOLEAN_LITERAL: "true" | "false" >
    | <NULL_LITERAL:    "null" >         

    | <INTEGER_LITERAL: <DECIMAL_LITERAL> | <HEX_LITERAL> >
    |     <#DECIMAL_LITERAL: ["0"-"9"] (["0"-"9"])*> 
    |       <#HEX_LITERAL: "0" ["x","X"] (["0"-"9","a"-"f","A"-"F"])+> 

    | <FLOATING_POINT_LITERAL:
        (["+","-"])? (["0"-"9"])+ "." (["0"-"9"])* (<EXPONENT>)?
      | "." (["0"-"9"])+ (<EXPONENT>)?
      | (["0"-"9"])+ <EXPONENT>
      | (["0"-"9"])+ (<EXPONENT>)?
      >
    |     <#EXPONENT: ["e","E"] ((["+","-"])? (["0"-"9"])+)? >
       
    | <STRING_LITERAL: <DOUBLE_QUOTE> | <SINGLE_QUOTE> >
    |     <#DOUBLE_QUOTE: 
          "\"" 
          (   (~["\"","\\","\n","\r"])
            | ("\\"
                ( ["n","t","b","r","f","\\","\""]
                | ["0"-"7"] ( ["0"-"7"] )?
                | ["0"-"3"] ["0"-"7"] ["0"-"7"]
                | "u" (["0"-"9","a"-"f","A"-"F"])+
                | "x" (["0"-"9","a"-"f","A"-"F"])+
                | "X" (["0"-"9","a"-"f","A"-"F"])+
                )
              )
          )*
          "\"">
    |     <#SINGLE_QUOTE: 
          "'" 
          (   (~["'","\\","\n","\r"])
            | ("\\"
                ( ["n","t","b","r","f","\\","'","\""]
                | ["0"-"7"] ( ["0"-"7"] )?
                | ["0"-"3"] ["0"-"7"] ["0"-"7"]
                | "u" (["0"-"9","a"-"f","A"-"F"])+
                | "x" (["0"-"9","a"-"f","A"-"F"])+
                | "X" (["0"-"9","a"-"f","A"-"F"])+
                )
              )
          )*
          "'">
}

TOKEN : /* Operators */
{ 
        < ASSIGN: "=" >
    |   < ASSIGN_ADD: "+=" >
    |   < ASSIGN_SUB: "-=" >
    |   < ASSIGN_MUL: "*=" >
    |   < ASSIGN_DIV: "/=" >
    |   < ASSIGN_MOD: "%=" >
    |   < ASSIGN_LSL: "<<=" >
    |   < ASSIGN_ASR: ">>=" >
    |   < ASSIGN_LSR: ">>>=" >
    |   < ASSIGN_AND: "&=" >
    |   < ASSIGN_OR: "|=" >
    |   < ASSIGN_XOR: "^=" >
    |   < LOGICAL_AND: "&&" >
    |   < LOGICAL_OR: "||" >
    |   < LOGICAL_NOT: "!" >
    |   < EQ: "==" >
    |   < GT: ">" >
    |   < LT: "<" >
    |   < GTE: ">=" >
    |   < LTE: "<=" >
    |   < NE: "!=" >
    |   < BIT_AND: "&" >
    |   < BIT_OR: "|" >
    |   < BIT_XOR: "^" >
    |   < BIT_NOT: "~" >
    |   < LSL: "<<" >
    |   < LSR: ">>>" >
    |   < ASR: ">>" >
    |   < PLUS: "+" >
    |   < MINUS: "-" >
    |   < MULTIPLY: "*" >
    |   < DIVIDE: "/" >
    |   < MOD: "%" >
    |   < INC: "++" >
    |   < DEC: "--" >
    |   < STRICT_EQ: "===" >
    |   < STRICT_NEQ: "!==" >
    |   < ADD: "add" >
    |   < AND: "and" >
    |   < OR: "or" >
    |   < STRING_EQ: "eq" >
    |   < STRING_NEQ: "ne" >
    |   < STRING_LE: "le" >
    |   < STRING_GT: "gt" >
    |   < STRING_GE: "ge" >
}

TOKEN : /* Commands */
{ 
      <IF: "if" >
    | <ELSE: "else" >
    | <WHILE: "while" >
    | <DO: "do" >
    | <FOR: "for" >
    | <IN: "in" >
    | <BREAK: "break" >
    | <CONTINUE: "continue" >
    | <DELETE: "delete" >
    | <INSTANCEOF: "instanceof" >
    | <RETURN: "return" >
    | <WITH: "with" >
    | <NEW: "new">
    | <FUNCTION: "function">
    | <VAR: "var">
    | <ONCLIPEVENT: "onClipEvent">
    | <ON: "on">
    | <TRY: "try">
    | <CATCH: "catch">
    | <FINALLY: "finally">
    | <THROW: "throw" >
    | <SWITCH: "switch">
    | <CASE: "case">
    | <DEFAULT_CASE: "default">
}

TOKEN : /* IDENTIFIERS */
{
      <IDENTIFIER: <LETTER> (<LETTER>|<DIGIT>)* >
    |     < #LETTER: [ "a"-"z", "A"-"Z", "_"] >
    |     < #DIGIT: [ "0"-"9"] >
}

TOKEN : /* PUNCTUATION */
{
      <SEMICOLON: ";" >
    | <LBRACE: "{" >
    | <RBRACE: "}" >
}

Node Literal() : {}
{
    <BOOLEAN_LITERAL> {
        return new Node(NodeType.BOOLEAN, token.image.toLowerCase());
    }
    
    | <INTEGER_LITERAL> {
        return new Node(NodeType.INTEGER, token.image);
    }
    
    | <FLOATING_POINT_LITERAL> {
        return new Node(NodeType.DOUBLE, token.image);
    }

    | <STRING_LITERAL> {
        return new Node(NodeType.STRING, token.image.substring(1, token.image.length()-1));
    }

    | <NULL_LITERAL> {
        return new Node(NodeType.NULL);
    }
}

Node Identifier() : { Token t; Node node = null; }
{
    /* Identifier() now looks ahead to see whether an identifier is followed 
     * by a Function() node. This allows 'named' functions to be diferentiated, 
     * e.g. stopAllSounds() from constructs such as 'this["echo"](args)'. This 
     * latter construct is why function calls are classed as suffix nodes since
     * any value returned in Flash can in theory be used to call a function.
     *
     * The Identifier() node short-circuits any PrimarySuffix() rule that might
     * follow by consuming the following tokens.
     */
      LOOKAHEAD(<IDENTIFIER> Function()) t=<IDENTIFIER> node=Function() { 
          node.setValue(t.image);
          return node; 
      }
    | LOOKAHEAD(<IDENTIFIER>) <IDENTIFIER> {
		  return new Node(NodeType.IDENTIFIER, token.image); 
      }
}

JAVACODE
void error_skipto(int kind) {

    ParseException e = generateParseException();
    errors.add(new ScriptError(ScriptError.Type.SCRIPT_PARSE_ERROR, path, 
        token.beginLine, token.endLine, token.beginColumn, token.endColumn));
    
    Token t;
  
    do {
        t = getNextToken();
    } while (t.kind != kind);
}

/*
 * Statements
 */
 
Node Script() : { Node root = new Node(NodeType.ARRAY), node; } 
{
    ( node=Statement() 
    { 
        if (node.getType() == NodeType.ONCLIPEVENT) {
            root.setType(NodeType.MOVIE_CLIP);
        } else if (node.getType() == NodeType.ON) {
            root.setType(NodeType.BUTTON);
        }
        root.add(node); 
    }
    )*
    { return root; }
}

Node Statement() : { Node node = null; }
{
    /*
     * Lookaheads are required on each different type of statements to allow
     * the following pairs of structures to be differentiated:
     * 
     * 1. BlockStatements and AnonymousObjects - both are enclosed by braces.
     * 2. FunctionDefinitions and MethodDefinitions - both start with the 'function'
     *
     * The ambiguities are a result of syntax choices in the design of the ActionScript
     * languages rather than shortcomings in the design of the parser.
     */
    try {
      LOOKAHEAD(BlockStatement()) node=BlockStatement() { return node; }
    | LOOKAHEAD(FunctionDefinition()) node=FunctionDefinition() { return node; }
    | LOOKAHEAD(ExpressionStatement()) node=ExpressionStatement() { return node; }
    | LOOKAHEAD(EmptyStatement()) node=EmptyStatement() { return node; }
    | LOOKAHEAD(IfStatement()) node=IfStatement() { return node; }
    | LOOKAHEAD(WithStatement()) node=WithStatement() { return node; }
    | LOOKAHEAD(WhileStatement()) node=WhileStatement() { return node; }
    | LOOKAHEAD(DoStatement()) node=DoStatement() { return node; }
    | LOOKAHEAD(ForStatement()) node=ForStatement() { return node; }
    | LOOKAHEAD(BreakStatement()) node=BreakStatement() { return node; }
    | LOOKAHEAD(ContinueStatement()) node=ContinueStatement() { return node; }
    | LOOKAHEAD(ReturnStatement()) node=ReturnStatement() { return node; }
    | LOOKAHEAD(OnClipEvent()) node=OnClipEvent() { return node; }
    | LOOKAHEAD(On()) node=On() { return node; }
    | LOOKAHEAD(TryStatement()) node=TryStatement() { return node; }
    | LOOKAHEAD(ThrowStatement()) node=ThrowStatement() { return node; }
    | LOOKAHEAD(SwitchStatement()) node=SwitchStatement() { return node; }
    } catch (ParseException e) {
        error_skipto(SEMICOLON);
    }
}

Node FunctionDefinition() : { Token t; Node node = new Node(NodeType.DEFINE_FUNCTION), name = null, args = null, body = null; }
{
    [<FUNCTION>] t=<IDENTIFIER> { node = new Node(NodeType.DEFINE_FUNCTION, t.image); } "(" [ args=ArgumentList() { node.add(args); } ] ")" body=BlockStatement() { node.add(body); }

    { return node; }
}

Node BlockStatement() : { Node list = new Node(NodeType.STATEMENT_LIST), node = null; }
{
    "{" ( node=Statement() { list.add(node); } )* "}"
    { return list; }
}

Node ExpressionStatement() : { Node node; }
{
    node=ExpressionList() (LOOKAHEAD(2) ";")? { return node; }
}

Node EmptyStatement() : {}
{
    ";" { return new Node(NodeType.NO_OP); }
}

Node IfStatement() : { Token t; Node ifnode = new Node(NodeType.IF), node = null;  ifnode.add(new Node(NodeType.NO_OP)); }
{
    t=<IF> "(" node=ConditionalExpression() ")" { ifnode.set(0, node); } 
        node=Statement() { ifnode.add(node); } 
        [ LOOKAHEAD(2) <ELSE> node=Statement() { ifnode.add(node); } ]

    { return ifnode; } 
}

Node WithStatement() : { Node expr; Node body; }
{
    <WITH> "(" expr=ConditionalExpression() ")" body=Statement()        
    { return new Node(NodeType.WITH).add(expr).add(body); }
}

Node WhileStatement() : { Node whileNode = new Node(NodeType.WHILE), node = null; whileNode.add(new Node(NodeType.NO_OP)); }
{
    <WHILE> "(" node=ConditionalExpression() ")" { whileNode.set(0, node); }
        node=Statement() { whileNode.add(node); }
        
    { return whileNode; }
}    

Node DoStatement() : { Node doNode = new Node(NodeType.DO), node = null; doNode.add(new Node(NodeType.NO_OP)); }
{
    <DO> node=Statement() { doNode.set(0, node); }
    <WHILE> "(" node=ConditionalExpression() ")" { doNode.add(node); }
    
    { return doNode; }
}    

Node ForStatement() : { Token t; Node forNode = null, node = null; }
{
    LOOKAHEAD (5) // Lookahead for the <IN> token that separates for() from for..in
    
      <FOR> 
          { 
              forNode = new Node(NodeType.FOR); 
              forNode.add(new Node(NodeType.NO_OP)); 
              forNode.add(new Node(NodeType.NO_OP)); 
              forNode.add(new Node(NodeType.NO_OP)); 
              forNode.add(new Node(NodeType.NO_OP)); 
          } "(" 
          [ node=ExpressionList() { forNode.set(0, node); } ] ";" 
          [ node=ConditionalExpression() { forNode.set(1, node); } ] ";" 
          [ node=ExpressionList() { forNode.set(2, node); } ] ")" 
          node=Statement() { forNode.set(3, node); return forNode; }
          
    | <FOR> { forNode = new Node(NodeType.FORIN); } "(" 
          ["var"] t=<IDENTIFIER> { forNode.add(new Node(NodeType.IDENTIFIER, t.image)); }
          <IN> node=ConditionalExpression() ")"  { forNode.add(node); }
          node=Statement() { forNode.add(node); return forNode; }
}

Node BreakStatement() : {}
{
    <BREAK> (LOOKAHEAD(";") ";")? { 
        return new Node(NodeType.BREAK); 
    }
}

Node ContinueStatement() : {}
{
    <CONTINUE> (LOOKAHEAD(";") ";")? { 
        return new Node(NodeType.CONTINUE); 
    }
}

Node ReturnStatement() : { Node returnNode = new Node(NodeType.RETURN), node = null; }
{
    <RETURN> [ node=ConditionalExpression() { returnNode.add(node); } ] (LOOKAHEAD(";") ";")?
    { return returnNode; }
}

EventNode OnClipEvent() : { Token t; EventNode eventNode = new EventNode(NodeType.ONCLIPEVENT); Node node = null; }
{
    t=<ONCLIPEVENT> "(" ( LOOKAHEAD(2) node=Identifier() node=Literal() | node=Identifier() )
    {
        if (node.getType() == NodeType.IDENTIFIER)
        {
            Event event = EventNode.getEvent(node.getValue().toLowerCase());
            if (event != null) {
                eventNode.addEvent(event); 
            }
            else {
                reportError("UnknownButtonEvent", t);
            }
        }
        else if (node.getType() == NodeType.STRING)
        {
            Integer key = EventNode.getKey(node.getValue().toLowerCase());
            
            if (key == null) {
                key =  Integer.valueOf(node.getValue().charAt(0));
            }
            eventNode.setKey(key); 
        }
        else
        {
            reportError("UnknownButtonEvent", t);
        }
       } 
    ( ","  ( LOOKAHEAD(2) node=Identifier() node=Literal() | node=Identifier() )
    {
        if (node.getType() == NodeType.IDENTIFIER)
        {
            Event event = EventNode.getEvent(node.getValue());
            if (event != null) {
                eventNode.addEvent(event); 
            }
            else {
                reportError("UnknownButtonEvent", t);
            }
        }
        else if (node.getType() == NodeType.STRING)
        {
           Integer key = EventNode.getKey(node.getValue());
            
            if (key == null) {
                key = Integer.valueOf(node.getValue().charAt(0));
            }
            eventNode.setKey(key); 
        }
        else
        {
            reportError("UnknownButtonEvent", t);
        }
    } 
    )* ")" node=Statement() { eventNode.add(node); }
        
    { return eventNode; }
}

EventNode On() : { Token t; EventNode eventNode = new EventNode(NodeType.ON); Node node = null; }
{
    t=<ON> "(" ( LOOKAHEAD(2) node=Identifier() node=Literal() | node=Identifier() )
    {
        if (node.getType() == NodeType.IDENTIFIER)
        {
            Event event = EventNode.getEvent(node.getValue().toLowerCase());
            if (event != null) {
                eventNode.addEvent(event); 
            }
            else {
                reportError("UnknownButtonEvent", t);
            }
        }
        else if (node.getType() == NodeType.STRING)
        {
            Integer key = EventNode.getKey(node.getValue().toLowerCase());
            
            if (key == null) {
                key =  Integer.valueOf(node.getValue().charAt(0));
            }
            eventNode.setKey(key); 
        }
        else
        {
            reportError("UnknownButtonEvent", t);
        }
       } 
    ( ","  ( LOOKAHEAD(2) node=Identifier() node=Literal() | node=Identifier() )
    {
        if (node.getType() == NodeType.IDENTIFIER)
        {
            Event event = EventNode.getEvent(node.getValue());
            if (event != null) {
                eventNode.addEvent(event); 
            }
            else {
                reportError("UnknownButtonEvent", t);
            }
        }
        else if (node.getType() == NodeType.STRING)
        {
           Integer key = EventNode.getKey(node.getValue());
            
            if (key == null) {
                key = Integer.valueOf(node.getValue().charAt(0));
            }
            eventNode.setKey(key); 
        }
        else
        {
            reportError("UnknownButtonEvent", t);
        }
    } 
    )* 
    ")" node=Statement() { eventNode.add(node); }
        
    { return eventNode; }
}

Node TryStatement() : { Node exceptionNode = new Node(NodeType.EXCEPTION), node = null, catchNode = null;}
{   
    <TRY>
        node=Statement() { Node tryNode = new Node(NodeType.TRY); tryNode.add(node); exceptionNode.add(tryNode); }
    
    ( LOOKAHEAD(2) <CATCH> "(" node=ConditionalExpression() ")" { catchNode = new Node(NodeType.CATCH); exceptionNode.add(catchNode);}
        node=Statement() { catchNode.add(node); } )*
    [ LOOKAHEAD(2) <FINALLY>
        node=Statement() { Node finallyNode = new Node(NodeType.FINALLY); finallyNode.add(node); exceptionNode.add(finallyNode); } ]
        
    { return exceptionNode; }
}    

Node ThrowStatement() : { Node node; }
{
    <THROW> node=ConditionalExpression() (LOOKAHEAD(";") ";")?
    { return new Node(NodeType.THROW).add(node); }
}

Node SwitchStatement() : { Node switchNode = new Node(NodeType.SWITCH), label = null, list = null, node = null; }
{
    <SWITCH> "(" node=ConditionalExpression() ")" { switchNode.add(node); } "{"
        (label=SwitchLabel() { switchNode.add(label); list = new Node (NodeType.LIST); } (node=Statement() {list.add(node); } )* { if (list.count() > 0) switchNode.add(list); } )*
    "}"
    
    { return switchNode; }
}    

Node SwitchLabel() : { Node node = new Node(NodeType.LABEL), expr = null; }
{
    ( <CASE> expr=Expression() ":" { node.add(expr); }
    | <DEFAULT_CASE> ":"
    )
    { return node; }
}

/*
 * Define lists
 */

Node ExpressionList() : { Node block = new Node(NodeType.LIST), node = null; }
{
    node=Expression() { block.add(node); } ( "," node=Expression() { block.add(node); } )*
    { 
        if (block.count() == 1)
            return block.get(0);
        else
            return block; 
    }
}

Node ArgumentList() : { Node block = new Node(NodeType.LIST), node = null; }
{
    node=ConditionalExpression() { block.add(node); } ( "," node=ConditionalExpression() { block.add(node); } )*
    { 
        if (block.count() == 1)
            return block.get(0);
        else
            return block; 
    }
}

/*
 * Expression Syntax
 */
 
Node Expression() : { Node node; }
{
      node=AssignmentExpression() { return node; }
}

Node AssignmentExpression() : { boolean isVar = false; NodeType type; Node node = null, left = null, right = null; }
{
    ( "var" { isVar = true; } ) ? node=ConditionalExpression()  
    { if (isVar) { node.setType(NodeType.DEFINE_VARIABLE); } }
    [
        ( <ASSIGN> { type = NodeType.ASSIGN; }
        | <ASSIGN_ADD> { type = NodeType.ASSIGN_ADD; }
        | <ASSIGN_SUB> { type = NodeType.ASSIGN_SUB; }
        | <ASSIGN_MUL> { type = NodeType.ASSIGN_MUL; }
        | <ASSIGN_DIV> { type = NodeType.ASSIGN_DIV; }
        | <ASSIGN_MOD> { type = NodeType.ASSIGN_MOD; }
        | <ASSIGN_LSL> { type = NodeType.ASSIGN_LSL; }
        | <ASSIGN_ASR> { type = NodeType.ASSIGN_ASR; }
        | <ASSIGN_LSR> { type = NodeType.ASSIGN_LSR; }
        | <ASSIGN_AND> { type = NodeType.ASSIGN_BIT_AND; }
        | <ASSIGN_OR> { type = NodeType.ASSIGN_BIT_OR; }
        | <ASSIGN_XOR> { type = NodeType.ASSIGN_BIT_XOR; }
        )  
        
        right=AssignmentExpression() 
    
        { 
            node = new Node(type).add(node).add(right);
        }
    
    ]
    
    { return node; }
}

/*
 * The following nodes describe a binary tree which are used to parse expressions containing 
 * the different types of operators. The left branch of the tree descends down one level.
 * The right branch parses the remaining part of the expression starting at the same level. 
 * This allows statements such as a + b * c to be correctly parsed as a + (b * c) which takes
 * into account the level of precedence assigned to different operations. If the right branch 
 * also descended down the tree then the expression would be parsed as (a + b) * c.
 */
 
Node ConditionalExpression() : { Node node = null, cond = null, a = null, b = null; }
{
    node=LogicalOrExpression() (  
        "?" a=Expression() ":" b=ConditionalExpression() 
        { cond = node; 
          node = new Node(NodeType.SELECT); 
          node.add(cond);
          node.add(a);
          node.add(b);
        }
    )?
    { return node; }
}

Node LogicalOrExpression() : { NodeType type; Node left, right; }
{
    left=LogicalAndExpression()  (
        ( <LOGICAL_OR> { type = NodeType.LOGICAL_OR; }
        | <OR> { type = NodeType.OR; }
        )
        right=LogicalAndExpression()
        
        { left = new Node(type).add(left).add(right); }
    )*
    { return left; }
}

Node LogicalAndExpression() : { NodeType type; Node left, right; }
{
    left=InclusiveOrExpression()  ( 
        ( <LOGICAL_AND> { type = NodeType.LOGICAL_AND; } 
        | <AND> { type = NodeType.AND; } 
        )
        right=InclusiveOrExpression()
        
        { left = new Node(type).add(left).add(right); }
    )*
    { return left; }
}

Node InclusiveOrExpression() : { Node left, right; }
{
    left=ExclusiveOrExpression() ( 
        <BIT_OR> right=ExclusiveOrExpression()
        { left = new Node(NodeType.BIT_OR).add(left).add(right); }
    )*
    { return left; }
}

Node ExclusiveOrExpression() : { Node left, right; }
{
    left=AndExpression() ( 
        <BIT_XOR> right=AndExpression()
        { left = new Node(NodeType.BIT_XOR).add(left).add(right); }
    )*
    { return left; }
}

Node AndExpression() : { Node left, right; }
{
    left=EqualityExpression() ( 
        <BIT_AND> right=EqualityExpression()
        { left = new Node(NodeType.BIT_AND).add(left).add(right); }
    )*
    { return left; }
}

Node EqualityExpression() : { NodeType type; Node left, right; }
{
    left=RelationalExpression() ( 
        ( <EQ> { type = NodeType.EQUAL; }
        | <NE> { type = NodeType.NOT_EQUAL; }
        | <STRICT_EQ> { type = NodeType.STRICT_EQUAL; }
        | <STRICT_NEQ> { type = NodeType.STRICT_NOT_EQUAL; }
        | <STRING_EQ> { type = NodeType.STRING_EQUAL; }
        | <STRING_NEQ> { type = NodeType.STRING_NOT_EQUAL; }
        ) 
        right=RelationalExpression()
 
        { left = new Node(type).add(left).add(right); }
    )*
    { return left; }
}

Node RelationalExpression() : { NodeType type; Node left, right; }
{
    left=InstanceExpression() ( 
        ( <LT> { type = NodeType.LESS_THAN; }
        | <GT> { type = NodeType.GREATER_THAN; }
        | <LTE> { type = NodeType.LESS_THAN_EQUAL; }
        | <GTE> { type = NodeType.GREATER_THAN_EQUAL; }
        | <STRING_GT> { type = NodeType.STRING_GREATER_THAN; }
        | <STRING_LE> { type = NodeType.STRING_LESS_THAN_EQUAL; }
        | <STRING_GE> { type = NodeType.STRING_GREATER_THAN_EQUAL; }
        ) 
        right=InstanceExpression()
 
        { left = new Node(type).add(left).add(right); }
    )*
    { return left; }
}

Node InstanceExpression() : { Node left, right; }
{
    left=ShiftExpression() ( 
        <INSTANCEOF> right=ShiftExpression()
        { left = new Node(NodeType.INSTANCEOF).add(left).add(right); }
    )*
    { return left; }
}

Node ShiftExpression() : { NodeType type; Node left, right; }
{
    left=AdditiveExpression() ( 
        ( <LSL> { type = NodeType.LSL; }
        | <LSR> { type = NodeType.LSR; }
        | <ASR> { type = NodeType.ASR; }
        ) right=AdditiveExpression()
        { left = new Node(type).add(left).add(right); }
    )*
    { return left; }
}

Node AdditiveExpression() : { NodeType type; Node left, right; }
{
    left=MultiplicativeExpression() ( 
        ( <PLUS> { type = NodeType.ADD; }
        | <MINUS> { type = NodeType.SUB; }
        | <ADD> { type = NodeType.STRING_ADD; }
        ) right=MultiplicativeExpression()
        { left = new Node(type).add(left).add(right); }
    )*
    { return left; }
}

Node MultiplicativeExpression() : { NodeType type; Node left, right; }
{
    left=UnaryExpression() ( 
        ( <MULTIPLY> { type = NodeType.MUL; }
        | <DIVIDE> { type = NodeType.DIV; }
        | <MOD> { type = NodeType.MOD; }
        ) right=UnaryExpression()
        { left = new Node(type).add(left).add(right); }
    )*
    { return left; }
}

Node UnaryExpression() : { NodeType type; Node node; }
{
      ( <PLUS> { type = NodeType.PLUS; } | <MINUS> { type = NodeType.MINUS; } ) node=UnaryExpression() 
      { return new Node(type).add(node); }
    | ( <INC> { type = NodeType.PRE_INC; } | <DEC> { type = NodeType.PRE_DEC; } ) node=PrimaryExpression()  
      { return new Node(type).add(node); }
    | ( <BIT_NOT> { type = NodeType.BIT_NOT; } | <LOGICAL_NOT> { type = NodeType.NOT; } ) node=UnaryExpression() 
      { return new Node(type).add(node); }
    | <DELETE> node=UnaryExpression()  
      { return new Node(NodeType.DELETE).add(node); }
    | node=PostfixExpression() { return node; }
}

Node PostfixExpression() : { NodeType type; Node node; }
{
    node=PrimaryExpression() (
        ( <INC> { type = NodeType.POST_INC; } 
        | <DEC> { type = NodeType.POST_DEC; } 
        ) { node = new Node(type).add(node); }
    )*
    { return node; }
}

/*
 * Primary expressions are split into a prefix/suffix pair. This allows some of the more 
 * esoteric features of ActionScript to be handled cleanly, specifically variables and 
 * array elements can contain objects and even functions. The statement a[1](arg) is 
 * valid ActionScript - the first element of the array contains a function which is 
 * executed passing the argument, arg.
 *
 * Suffixing also allows object path names, e.g. a.b.c() to be correctly parsed.
 *
 * IMPORTANT: The Identifier() node looks ahead to see whether the identifier is 
 * actually the name of a function. The node looks ahead to see if the identifier 
 * token is followed by a Function() node. If so the tokens are consumed.
 */
Node PrimaryExpression() : { Node list = new Node(NodeType.VALUE), node; }
{
    node=PrimaryPrefix() { list.add(node); } ( node=PrimarySuffix() { list.add(node); } )*
    { 
        if (list.count() == 1)
            return list.get(0);
        else
            return list; 
    }
}

Node PrimaryPrefix() : { Node node; }
{
    LOOKAHEAD(6)
    
      node=AnonymousArray() { return node; }
    | node=AnonymousObject() { return node; }
    | node=MethodDefinition() { return node; }
    | node=Constructor() { return node; }
    | node=Literal() { return node; }
    | node=Identifier() { return node; }
    | "(" node=ConditionalExpression() ")" { return node; }
}

Node PrimarySuffix() : { Token t; Node node = null, name = null; }
{
    /*
     * Lookahead using the <IDENTIFIER> token when separating fetching an 
     * attribute from calling a method on an object rather than using the
     * Identifier() node. Look at Identifier() for an explanation why this
     * is the case.
     */
      "[" node=ConditionalExpression() "]" 
      { return new Node(NodeType.SUBSCRIPT).add(node); }
    | LOOKAHEAD ("." <IDENTIFIER> "(" ) "." t=<IDENTIFIER> node=Function() 
      { return new Node(NodeType.METHOD, t.image).addAll(node.getChildren()); }
    | LOOKAHEAD ("." <IDENTIFIER>) "." t=<IDENTIFIER> 
      { return new Node(NodeType.ATTRIBUTE, t.image); }
    | node=Function() 
      { return node; }
}

Node Function() : { Node node = new Node(NodeType.FUNCTION, ""), arg; }
{
    "(" [ arg=ConditionalExpression() { node.add(arg); } ( "," arg=ConditionalExpression() { node.add(arg); } )* ] ")"
    
    { return node; }
}

Node Constructor() : { Token t; List<Node>list = new ArrayList<Node>(); Node arg; }
{
    <NEW> 
    ( LOOKAHEAD(t=<IDENTIFIER> "(") t=<IDENTIFIER> "(" 
        [ arg=ConditionalExpression() { list.add(arg); } 
            ( "," arg=ConditionalExpression() { list.add(arg); } )* ] ")" 
    | LOOKAHEAD(t=<IDENTIFIER>) t=<IDENTIFIER>
    )
    { return new Node(NodeType.NEW_OBJECT, t.image).addAll(list); }
}

Node AnonymousArray() : { Node node = new Node(NodeType.DEFINE_ARRAY), element = null; }
{
    "[" [ element=ConditionalExpression() { node.add(element); } 
        ( "," element=ConditionalExpression() { node.add(element); } )*
        ] 
    "]"
    
    { return node; }
}

Node MethodDefinition() : { Node node = new Node(NodeType.DEFINE_METHOD), args = null, body = null; }
{
    <FUNCTION> "(" [ args=ArgumentList() { node.add(args); } ] ")" body=BlockStatement() { node.add(body); }

    { return node; }
}

Node AnonymousObject() : { Node node = new Node(NodeType.DEFINE_OBJECT), attribute = null; }
{
    "{" [ attribute=Attribute() { node.add(attribute); } ( "," attribute=Attribute() { node.add(attribute); } )* ] "}"
    
    { return node; }
}

Node Attribute() : { Node key = null, value = null; }
{
    key=Identifier() ":" value=ConditionalExpression()
    
    { return new Node(NodeType.DEFINE_ATTRIBUTE).add(key).add(value); }
}
